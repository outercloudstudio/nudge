apply plugin: 'java'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
  main {
    java.srcDirs = ["src/main"]
    java.includes = ["**/*.java"]
    resources.srcDirs = ["src/main"]
    resources.excludes = ["**/*.java"]

    java.destinationDirectory.set(file("$buildDir/classes"))
    output.resourcesDir = "$buildDir/classes"
  }
  test {
    java.srcDirs = ["src/test"]
    java.includes = ["**/*.java"]
    resources.srcDirs = ["src/test"]
    resources.excludes = ["**/*.java"]

    java.destinationDirectory.set(file("$buildDir/tests"))
    output.resourcesDir = "$buildDir/tests"
  }
}

repositories {
  mavenCentral()
  // Java Spatial Index for RTree
  maven {url "https://oss.sonatype.org/content/repositories/snapshots/"}
  maven {url "https://mvnrepository.com/artifact/net.sf.trove4j/trove4j"}
}

dependencies {
  implementation(
    [group: 'org.slf4j', name: 'slf4j-api', version: '2.0.6'],
    [group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.6'],
    [group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'],
    [group: 'commons-cli', name: 'commons-cli', version: '1.5.0'],
    [group: 'commons-io', name: 'commons-io', version: '2.11.0'],

    [group: 'org.ow2.asm', name: 'asm', version: '9.7.1'],
    [group: 'org.ow2.asm', name: 'asm-tree', version: '9.7.1'],

    // Flatbuffers
    [group: 'com.google.flatbuffers', name: 'flatbuffers-java', version: '24.3.25'],

    // Websockets
    [group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.3.0'],

    // We only use WeakIdentityHashMap which doesn't depend on anything
    [group: 'org.hibernate', name: 'hibernate-search', version: '3.1.0.GA'],

    // Java Spatial Index, RTree indexing
    // The official Maven repositories do not host net.sourceforge.jsi:jsi.
    // There are no valid released versions on Maven Central or Sonatype.
    // If you need JSI, download jsi-1.0.jar manually and use:
    [group: 'net.sf.trove4j', name: 'trove4j', version: '2.1.0'],
    
    [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'],
  )
  // implementation files('lib/jsi-1.0.jar')

  testImplementation(
    [group: 'junit', name: 'junit', version: '4.13.2'],
    [group: 'org.mockito', name: 'mockito-core', version: '3.12.4']
  )
}

jar {
  duplicatesStrategy 'exclude'
  from {
    configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
  }
}

javadoc {
  includes = ["**/common/**"]
  options.windowTitle = "Battlecode 2026"
  options.classpath = sourceSets.main.compileClasspath as List
  options.doclet = "jdk.javadoc.doclet.StandardDoclet"
  options.taglets = ["battlecode.doc.CostlyMethodTaglet"]
  options.tagletPath = ( sourceSets.main.output.classesDirs as List )+ sourceSets.main.compileClasspath
}
