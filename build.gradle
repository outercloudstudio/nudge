apply plugin: 'java'
apply plugin: 'maven-publish'

import java.nio.file.*

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

if(JavaVersion.current() < JavaVersion.VERSION_21) {
    throw new GradleException("The engine must be run using >= Java 21 JDK")
}

// Avoid weird configuration-time dependency bugs
// Fun fact: this line of code single-handedly fixed an error I spent two hours debugging.
evaluationDependsOnChildren()

configurations {
    scala

    engine {
      transitive false
    }
    bots {
      transitive false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    scala group: 'org.scala-lang', name: 'scala-library', version: '2.11.7'
    scala group: 'org.scala-lang', name: 'scala-compiler', version: '2.11.7'
    scala group: 'org.scala-lang', name: 'scala-reflect', version: '2.11.7'

    engine project(":engine")
    bots project(":example-bots")
}

task installPythonPackage(type: Exec) {
  if (project.findProperty('forceInstallPythonPackage') ?: false) {
    commandLine 'python', '-m', 'pip', 'install', '-e', 'engine/src/crossplay_python'
  } else {
    commandLine 'python', '-c', "import importlib.util,subprocess,sys; assert sys.version_info[0] == 3 and sys.version_info[1] == 12, 'Error: Must use Python 3.12!'; pkg='battlecode26'; importlib.util.find_spec(pkg) or subprocess.check_call([sys.executable,'-m','pip','install','-e','engine/src/crossplay_python'])"
  }
}

def serverJar = configurations.engine.singleFile

task buildMap(type: JavaExec, dependsOn: [':engine:build']) {
    mainClass = 'battlecode.world.maps.' + project.property('buildMap')
    classpath = files(serverJar)
}

task buildMaps(type: JavaExec, dependsOn: [':engine:build']) {
  mainClass = 'battlecode.world.BuildMaps'
  classpath = files(serverJar)
}

def defaultClassLocation = project(':example-bots').sourceSets.main.output.classesDirs.getAsPath()
def defaultReplay = 'matches/' + project.property('teamA') + '-vs-' + project.property('teamB') + '-on-' + project.property('maps') + new Date().format('yyyyMMddHHmmss') + '.bc6'
def saveFileName = (project.findProperty('replay') ?: defaultReplay)
saveFileName = saveFileName.substring(0, saveFileName.length() - 5)
saveFileName = saveFileName.substring(0, Math.min(saveFileName.length(), 100)) + '.bc26'

task headless(type: JavaExec, dependsOn: [':engine:build', ':example-bots:build']) {
  mainClass = 'battlecode.server.Main'
  classpath = files(serverJar) + project(':example-bots').sourceSets.main.output + configurations.scala
  args = ['-c=-']
  jvmArgs = [
    '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.math=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.util=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.access=ALL-UNNAMED',
    '--add-opens=java.base/sun.security.action=ALL-UNNAMED',
    '-Dbc.server.wait-for-client=' + (project.findProperty('waitForClient') ?: 'false'),
    '-Dbc.server.mode=headless',
    '-Dbc.server.map-path=maps',
    '-Dbc.server.robot-player-to-system-out=' + (project.findProperty('outputVerbose') ?: 'true'),
    '-Dbc.server.debug=' + (project.findProperty('debug') ?: 'false'),
    '-Dbc.engine.debug-methods=' + (project.findProperty('debug') ?: 'false'),
    '-Dbc.engine.enable-profiler=' + (project.findProperty('enableProfiler') ?: 'false'),
    '-Dbc.engine.show-indicators=' + (project.findProperty('showIndicators') ?: 'true'),
    '-Dbc.game.team-a=' + project.property('teamA'),
    '-Dbc.game.team-b=' + project.property('teamB'),
    '-Dbc.game.team-a.language=' + (project.findProperty('languageA') ?: 'java'),
    '-Dbc.game.team-b.language=' + (project.findProperty('languageB') ?: 'java'),
    '-Dbc.game.team-a.url=' + (project.findProperty('classLocationA') ?: defaultClassLocation),
    '-Dbc.game.team-b.url=' + (project.findProperty('classLocationB') ?: defaultClassLocation),
    '-Dbc.game.team-a.package=' + (project.findProperty('packageNameA') ?: project.property('teamA')),
    '-Dbc.game.team-b.package=' + (project.findProperty('packageNameB') ?: project.property('teamB')),
    '-Dbc.game.maps=' + project.property('maps'),
    '-Dbc.server.validate-maps=' + project.property('validateMaps'),
    '-Dbc.server.alternate-order=' + project.property('alternateOrder'),
    '-Dbc.server.save-file=' + (saveFileName)
  ]
}

task headlessNoBuild(type: JavaExec) {
  mainClass = 'battlecode.server.Main'
  classpath = files(serverJar) + project(':example-bots').sourceSets.main.output + configurations.scala
  args = ['-c=-']
  jvmArgs = [
    '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.math=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.util=ALL-UNNAMED',
    '--add-opens=java.base/jdk.internal.access=ALL-UNNAMED',
    '--add-opens=java.base/sun.security.action=ALL-UNNAMED',
    '-Dbc.server.wait-for-client=' + (project.findProperty('waitForClient') ?: 'false'),
    '-Dbc.server.mode=headless',
    '-Dbc.server.map-path=maps',
    '-Dbc.server.robot-player-to-system-out=' + (project.findProperty('outputVerbose') ?: 'true'),
    '-Dbc.server.debug=' + (project.findProperty('debug') ?: 'false'),
    '-Dbc.engine.debug-methods=' + (project.findProperty('debug') ?: 'false'),
    '-Dbc.engine.enable-profiler=' + (project.findProperty('enableProfiler') ?: 'false'),
    '-Dbc.engine.show-indicators=' + (project.findProperty('showIndicators') ?: 'true'),
    '-Dbc.game.team-a=' + project.property('teamA'),
    '-Dbc.game.team-b=' + project.property('teamB'),
    '-Dbc.game.team-a.language=' + (project.findProperty('languageA') ?: 'java'),
    '-Dbc.game.team-b.language=' + (project.findProperty('languageB') ?: 'java'),
    '-Dbc.game.team-a.url=' + (project.findProperty('classLocationA') ?: defaultClassLocation),
    '-Dbc.game.team-b.url=' + (project.findProperty('classLocationB') ?: defaultClassLocation),
    '-Dbc.game.team-a.package=' + (project.findProperty('packageNameA') ?: project.property('teamA')),
    '-Dbc.game.team-b.package=' + (project.findProperty('packageNameB') ?: project.property('teamB')),
    '-Dbc.game.maps=' + project.property('maps'),
    '-Dbc.server.validate-maps=' + project.property('validateMaps'),
    '-Dbc.server.alternate-order=' + project.property('alternateOrder'),
    '-Dbc.server.save-file=' + (saveFileName)
  ]
}

task crossPlayPy(type: Exec, dependsOn: ['installPythonPackage']) {
  commandLine 'python', '-m', 'battlecode26',
  '--teamA', (project.findProperty('languageA') == 'java' ? '/' : project.property('teamA')),
  '--teamB', (project.findProperty('languageB') == 'java' ? '/' : project.property('teamB')),
  '--dirA', 'example-bots/src/crossplay_python',
  '--dirB', 'example-bots/src/crossplay_python',
  '--new-process'
}

// keep the client happy because it references this step
task unpackClient() {}

task run(dependsOn: ['unpackClient', 'headless']) {}

if (project.hasProperty('languageA') && project.property('languageA') == 'python' ||
    project.hasProperty('languageB') && project.property('languageB') == 'python') {
  run.dependsOn crossPlayPy
  headless.mustRunAfter crossPlayPy
}

task runClient {
  doLast {
    exec {
      commandLine 'npm', 'install'
      workingDir 'client/visualizer'
    }

    exec {
      commandLine 'npm', 'run', 'watch'
      workingDir 'client/visualizer'
    }
  }
}

task release(dependsOn: ['release_main', 'release_docs', 'release_sources'])

task release_main(type: Jar, dependsOn: [':engine:build']) {
  File f_version = new File(project.projectDir, "battlecode_version");

  doFirst {
    if (!project.hasProperty("release_version"))
      throw new InvalidUserDataException("Must provide property \"release_version\"")

    Files.write(f_version.toPath(), [project.property("release_version")]);
  }

  archiveBaseName.set("battlecode")
  if (project.hasProperty("release_version"))
    archiveVersion.set(project.property("release_version"))
  destinationDirectory.set(project.projectDir)

  FileCollection src = files(f_version);
  src += zipTree(serverJar);

  from src;

  doLast {
    Files.delete(f_version.toPath())
  }
}

task release_docs(type: Jar, dependsOn: [':engine:javadoc']) {
  doFirst {
    if (!project.hasProperty("release_version") || project.property("release_version") == "unspecified")
      throw new InvalidUserDataException("Must provide property \"release_version\"")
  }

  archiveBaseName.set("battlecode-javadoc")
  if (project.hasProperty("release_version"))
    archiveVersion.set(project.property("release_version"))
  destinationDirectory.set(project.projectDir)
  archiveClassifier.set('javadoc')

  from new File(project(":engine").docsDir, "javadoc")
}

task release_sources(type: Jar, dependsOn: classes) {
  archiveBaseName.set("battlecode-source")
  if (project.hasProperty("release_version"))
    archiveVersion.set(project.property("release_version"))
  destinationDirectory.set(project.projectDir)
  archiveClassifier.set('sources')

  from project(":engine").sourceSets.main.allSource
}

publishing {
  publications {
    server(MavenPublication) {
      groupId 'org.battlecode'
      artifactId project.findProperty('artifact_id') ?: 'NONSENSE'
      version project.findProperty('release_version') ?: 'NONSENSE'

      artifact release_main
      artifact release_docs
      artifact release_sources
    }

    client(MavenPublication) {
      groupId 'org.battlecode'
      artifactId project.findProperty('artifact_id') ?: 'NONSENSE'
      version project.findProperty('release_version') ?: 'NONSENSE'

      artifact project.findProperty('artifact_path') ?: 'NONSENSE'
    }
  }
}
